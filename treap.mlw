module Treap

    use int.Int

    type key = int
    type priority = int
    type treap = Leaf | Node treap key priority treap

    val hash (k : key) : priority

    (* self-defined predicates *)
    predicate treap_less (t : treap) (k : key) = 
        match t with 
          Leaf -> true 
        | (Node l k1 _ r) -> (treap_less l k) /\ (k1 < k) /\ (treap_less r k) 
        end 
    
    predicate treap_greater (t : treap) (k : key) = 
        match t with 
          Leaf -> true 
        | (Node l k1 _ r) -> (treap_greater l k) /\ (k1 > k) /\ (treap_greater r k) 
        end

    predicate contains (t : treap) (k : key) =
        match t with 
          Leaf -> false 
        | (Node l k1 _ r) -> k1 = k \/ contains l k \/ contains r k
        end
    
    predicate contains_p (t : treap) (k : key) (p : priority) =
        match t with 
          Leaf -> false 
        | (Node l k1 p1 r) -> (k1 = k /\ p1 = p) \/ contains l k \/ contains r k
        end
    
    predicate valid_treap (t : treap) = 
        match t with 
          Leaf -> true 
        | (Node l k p r) -> valid_treap l /\ treap_less l k
            /\ valid_treap r /\ treap_greater r k
            /\ (forall k1. (k1 < k /\ contains t k1) <-> contains l k1)
            /\ (forall k1. (k1 > k /\ contains t k1) <-> contains r k1)
            /\ (forall k1, p1. contains_p l k1 p1 -> p >= p1)
            /\ (forall k1, p1. contains_p r k1 p1 -> p >= p1)
        end

    (* split and join *)
    let rec split (t : treap) (k : key) : (treap, bool, treap) =
        requires {valid_treap t}
        variant {t}
        ensures {let (l, _, _) = result in forall k1. (k1 < k /\ contains t k1) <-> contains l k1}
        ensures {let (_, _, r) = result in forall k1. (k1 > k /\ contains t k1) <-> contains r k1}
        ensures {let (l, _, _) = result in valid_treap l /\ treap_less l k}
        ensures {let (_, _, r) = result in valid_treap r /\ treap_greater r k}
        ensures {let (_, b, _) = result in b <-> contains t k}
        ensures {let (l, _, r) = result in forall k1. contains l k1 -> not contains r k1}
        ensures {let (l, _, r) = result in forall k1. contains r k1 -> not contains l k1}
        match t with 
          Leaf -> (Leaf, false, Leaf)
        | (Node l k1 p r) ->
            if k = k1 then (l, true, r)
            else if k < k1 then
                let (ll, x, lr) = split l k in (ll, x, Node lr k1 p r)
            else
                let (rl, x, rr) = split r k in (Node l k1 p rl, x, rr)
        end 
    
    let rec join (t1 : treap) (t2 : treap) : treap =
        requires {valid_treap t1 /\ valid_treap t2}
        variant {t1, t2}
        ensures {valid_treap result}
        ensures {forall k1, k2. (contains t1 k1 /\ contains t2 k2) -> k1 < k2}
        ensures {forall k. (contains t1 k \/ contains t2 k) <-> contains result k}
        match (t1, t2) with 
          (Leaf, _) -> t2 
        | (_, Leaf) -> t1
        | (Node l1 k1 p1 r1, Node l2 k2 p2 r2) ->
            if (p1 > p2) then Node l1 k1 p1 (join r1 t2)
            else Node (join t1 l2) k2 p2 r2
        end

    (* treap functions *)
    

    let empty () : treap = 
        ensures {valid_treap result}
        ensures {forall k. contains result k = false}
        Leaf

    let singleton (k : key) : treap =
        ensures {valid_treap result}
        ensures {contains result k}
        ensures {forall k1. k1 <> k -> contains result k1 = false}
        Node Leaf k (hash k) Leaf

    let delete (t : treap) (k : key) : treap =
        requires {valid_treap t}
        ensures {valid_treap result}
        ensures {contains result k = false}
        ensures {forall k1. contains result k1 -> contains t k1}
        let (l, _, r) = split t k in 
        join l r

    let joinM (t1 : treap) (k : key) (t2 : treap) : treap = 
        requires {valid_treap t1 /\ valid_treap t2}
        ensures {valid_treap result}
        ensures {forall k1. (contains t1 k1 \/ contains t2 k1 \/ k1 = k) <-> contains result k1}
        ensures {contains result k}
        join t1 (join (singleton k) t2)

    let insert (t : treap) (k : key) : treap =
        requires {valid_treap t}
        ensures {valid_treap result}
        ensures {forall k1. contains result k1 <-> (k1 = k \/ contains t k1)}
        ensures {contains result k}
        let (l, _, r) = split t k in 
        joinM l k r

    let rec intersection (t1 : treap) (t2 : treap) : treap =
        requires {valid_treap t1 /\ valid_treap t2}
        variant {t1}
        ensures {valid_treap result}
        ensures {forall k1. (contains t1 k1 /\ contains t2 k1) <-> contains result k1}
        match (t1, t2) with
          (Leaf, _) -> Leaf 
        | (_, Leaf) -> Leaf 
        | (Node l1 k1 _ r1, _) ->
            let (l2, b, r2) = split t2 k1 in
            let l = intersection l1 l2 in
            let r = intersection r1 r2 in 
            if b then joinM l k1 r else join l r 
        end

    (* t1 - t2 *)
    let rec difference (t1 : treap) (t2 : treap) : treap =
        requires {valid_treap t1 /\ valid_treap t2}
        variant {t1}
        ensures {valid_treap result}
        ensures {forall k. (contains t1 k /\ (not contains t2 k)) <-> contains result k}
        match (t1, t2) with 
          (Leaf, _) -> Leaf 
        | (_, Leaf) -> t1 
        | (Node l1 k1 _ r1, _) ->
            let (l2, b, r2) = split t2 k1 in 
            let l = difference l1 l2 in 
            let r = difference r1 r2 in 
            if b then join l r else joinM l k1 r
        end 

    let rec union (t1 : treap) (t2 : treap) : treap =
        requires {valid_treap t1 /\ valid_treap t2}
        variant {t1}
        ensures {valid_treap result}
        ensures {forall k1. (contains t1 k1 \/ contains t2 k1) <-> contains result k1}
        match (t1 ,t2) with 
          (Leaf, _) -> t2 
        | (_, Leaf) -> t1 
        | (Node l1 k1 _ r1, _) ->
            let (l2, _, r2) = split t2 k1 in 
            let l = union l1 l2 in 
            let r = union r1 r2 in 
            joinM l k1 r
        end

end