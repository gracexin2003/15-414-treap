module ParametricBST

    use int.Int

    type key = int
    type tree = Leaf | Node tree key tree

    (* self-defined predicate/axioms *)
    predicate tree_less (t : tree) (k : key) = 
        match t with 
          Leaf -> true 
        | (Node l k1 r) -> (tree_less l k) /\ (k1 < k) /\ (tree_less r k) 
        end 
    
    predicate tree_greater (t : tree) (k : key) = 
        match t with 
          Leaf -> true 
        | (Node l k1 r) -> (tree_greater l k) /\ (k1 > k) /\ (tree_greater r k) 
        end
    
    predicate valid_tree (t : tree) = 
        match t with 
          Leaf -> true 
        | (Node l k r) -> (tree_less l k) /\ (tree_greater r k)
        end
    
    predicate contains (t : tree) (k : key) =
        match t with 
          Leaf -> false 
        | (Node l k1 r) -> k1 = k \/ contains l k \/ contains r k
        end

    (* split and join*)
    val split (t : tree) (k : key) : (tree, bool, tree)
    val join (t1 : tree) (t2 : tree) : tree

    (* axioms with split and join *)
    axiom valid_split: forall t k. let (l, _, r) = split t k
        in valid_tree l /\ valid_tree r /\ tree_less l k /\ tree_greater r k
    axiom valid_join: forall t1 t2. valid_tree (join t1 t2)

    (* tree functions *)
    let empty () : tree = 
        ensures {valid_tree result}
        Leaf

    let singleton (k : key) : tree =
        ensures {valid_tree result}
        Node Leaf k Leaf

    let delete (t : tree) (k : key) : tree =
        requires {valid_tree t}
        ensures {valid_tree result}
        let (l, _, r) = split t k in 
        join l r

    let joinM (t1 : tree) (k : key) (t2 : tree) : tree = 
        requires {valid_tree t1 /\ valid_tree t2}
        ensures {valid_tree result}
        join t1 (join (singleton k) t2)

    let insert (t : tree) (k : key) : tree =
        requires {valid_tree t}
        ensures {valid_tree result}
        let (l, _, r) = split t k in 
        joinM l k r

    let rec intersection (t1 : tree) (t2 : tree) : tree =
        requires {valid_tree t1 /\ valid_tree t2}
        variant {t1}
        ensures {valid_tree result}
        match (t1, t2) with
          (Leaf, _) -> Leaf 
        | (_, Leaf) -> Leaf 
        | (Node l1 k1 r1, _) ->
            let (l2, b, r2) = split t2 k1 in
            let l = intersection l1 l2 in
            let r = intersection r1 r2 in 
            if b then joinM l k1 r else join l r 
        end

    (* t1 - t2 *)
    let rec difference (t1 : tree) (t2 : tree) : tree =
        requires {valid_tree t1 /\ valid_tree t2}
        variant {t1}
        ensures {valid_tree result}
        match (t1, t2) with 
          (Leaf, _) -> Leaf 
        | (_, Leaf) -> t1 
        | (Node l1 k1 r1, _) ->
            let (l2, b, r2) = split t2 k1 in 
            let l = difference l1 l2 in 
            let r = difference r1 r2 in 
            if b then join l r else joinM l k1 r 
        end 

    let rec union (t1 : tree) (t2 : tree) : tree =
        requires {valid_tree t1 /\ valid_tree t2}
        variant {t1}
        ensures {valid_tree result}
        match (t1 ,t2) with 
          (Leaf, _) -> t2 
        | (_, Leaf) -> t1 
        | (Node l1 k1 r1, _) ->
            let (l2, _, r2) = split t2 k1 in 
            let l = union l1 l2 in 
            let r = union r1 r2 in 
            joinM l k1 r
        end

end