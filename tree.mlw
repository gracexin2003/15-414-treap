module ParametricBST

    use int.Int

    type key = int
    type tree = Leaf | Node tree key tree

    (* self-defined predicate/axioms *)
    predicate tree_less (t : tree) (k : key) = 
        match t with 
          Leaf -> true 
        | (Node l k1 r) -> (tree_less l k) /\ (k1 < k) /\ (tree_less r k) 
        end 
    
    predicate tree_greater (t : tree) (k : key) = 
        match t with 
          Leaf -> true 
        | (Node l k1 r) -> (tree_greater l k) /\ (k1 > k) /\ (tree_greater r k) 
        end

    predicate contains (t : tree) (k : key) =
        match t with 
          Leaf -> false 
        | (Node l k1 r) -> k1 = k \/ contains l k \/ contains r k
        end
    
    predicate valid_tree (t : tree) = 
        match t with 
          Leaf -> true 
        | (Node l k r) -> valid_tree l /\ tree_less l k /\ contains l k = false
            /\ valid_tree r /\ tree_greater r k /\ contains r k = false
            /\ (forall k1. (k1 < k /\ contains t k1) <-> contains l k1)
            /\ (forall k1. (k1 > k /\ contains t k1) <-> contains r k1)
            (* adding more conditions idk if this will help *)
            /\ (forall k1. contains l k1 -> not contains r k1)
            /\ (forall k1. contains r k1 -> not contains l k1)
        end

    (* split and join*)
    val split (t : tree) (k : key) : (tree, bool, tree)
    requires {valid_tree t}
    ensures {let (l, b, r) = result in forall k1. (k1 < k /\ contains t k1) <-> contains l k1}
    ensures {let (l, b, r) = result in forall k1. (k1 > k /\ contains t k1) <-> contains r k1}
    ensures {let (l, b, r) = result in valid_tree l /\ tree_less l k /\ contains l k = false}
    ensures {let (l, b, r) = result in valid_tree r /\ tree_greater r k /\ contains r k = false}
    ensures {let (l, b, r) = result in b <-> contains t k}
    ensures {let (l, b, r) = result in forall k1. contains l k1 -> not contains r k1}
    ensures {let (l, b, r) = result in forall k1. contains r k1 -> not contains l k1}

    val join (t1 : tree) (t2 : tree) : tree
    requires {valid_tree t1 /\ valid_tree t2}
    ensures {valid_tree result}
    ensures {forall k. (contains t1 k \/ contains t2 k) <-> contains result k}

    (* tree functions *)
    let empty () : tree = 
        ensures {valid_tree result}
        ensures {forall k. contains result k = false}
        Leaf

    let singleton (k : key) : tree =
        ensures {valid_tree result}
        ensures {contains result k}
        ensures {forall k1. k1 <> k -> contains result k1 = false}
        Node Leaf k Leaf

    let delete (t : tree) (k : key) : tree =
        requires {valid_tree t}
        ensures {valid_tree result}
        ensures {contains result k = false}
        let (l, _, r) = split t k in 
        join l r

    let joinM (t1 : tree) (k : key) (t2 : tree) : tree = 
        requires {valid_tree t1 /\ valid_tree t2}
        ensures {valid_tree result}
        ensures {forall k1. (contains t1 k1 \/ contains t2 k1 \/ k1 = k) <-> contains result k1}
        ensures {contains result k}
        join t1 (join (singleton k) t2)

    let insert (t : tree) (k : key) : tree =
        requires {valid_tree t}
        ensures {valid_tree result}
        ensures {forall k1. (k1 <> k /\ contains t k1) -> contains result k1}
        ensures {contains result k}
        let (l, _, r) = split t k in 
        joinM l k r

    let rec intersection (t1 : tree) (t2 : tree) : tree =
        requires {valid_tree t1 /\ valid_tree t2}
        variant {t1}
        ensures {valid_tree result}
        ensures {forall k1. (contains t1 k1 /\ contains t2 k1) -> contains result k1}
        match (t1, t2) with
          (Leaf, _) -> Leaf 
        | (_, Leaf) -> Leaf 
        | (Node l1 k1 r1, _) ->
            let (l2, b, r2) = split t2 k1 in
            let l = intersection l1 l2 in
            let r = intersection r1 r2 in 
            if b then joinM l k1 r else join l r 
        end

    (* t1 - t2 *)
    let rec difference (t1 : tree) (t2 : tree) : tree =
        requires {valid_tree t1 /\ valid_tree t2}
        variant {t1}
        ensures {valid_tree result}
        ensures {forall k. (contains t1 k /\ (not contains t2 k)) -> contains result k}
        ensures {forall k. (not contains t1 k) /\ (not contains t2 k) -> (not contains result k)}
        ensures {forall k. (not contains t1 k \/ contains t2 k) -> not contains result k}
        match (t1, t2) with 
          (Leaf, _) -> Leaf 
        | (_, Leaf) -> t1 
        | (Node l1 k1 r1, _) ->
            let (l2, b, r2) = split t2 k1 in 
            let l = difference l1 l2 in 
            let r = difference r1 r2 in 
            assert {not contains l1 k1};
            assert {not contains l2 k1};
            assert {not contains r1 k1};
            assert {not contains r2 k1};
            assert {not contains l k1};
            assert {not contains r k1};
            assert {forall k. (contains l1 k /\ (contains l2 k <-> false)) -> contains l k};
            assert {forall k. (contains r1 k /\ (contains r2 k <-> false)) -> contains r k};
            let tmp = join l r in 
            let tmp2 = joinM l k1 r in
            (* assert {forall k. (contains t1 k /\ not contains t2 k) -> contains tmp k}; *)
            assert {forall k. (k < k1 /\ contains t1 k) -> contains l1 k};
            assert {forall k. (k < k1 /\ not contains t2 k) -> not contains l2 k};
            assert {forall k. (k < k1 /\ contains t1 k /\ not contains t2 k) -> contains l k};
            assert {forall k. (k > k1 /\ contains t1 k /\ not contains t2 k) -> contains r k};
            assert {forall k. (k < k1 /\ contains t1 k /\ not contains t2 k) -> contains tmp k};
            assert {forall k. (k > k1 /\ contains t1 k /\ not contains t2 k) -> contains tmp k};
            assert {forall k. (k = k1 /\ contains t1 k /\ not contains t2 k) -> contains tmp k};
            assert {forall k. (k = k1 /\ b) -> (contains t2 k /\ not contains tmp k)};
            assert {forall k. (contains t1 k /\ not contains t2 k) -> contains tmp2 k};
            assert {b -> not contains tmp k1};
            assert {not b -> contains tmp2 k1};
            if b then join l r else joinM l k1 r
        end 

    let rec union (t1 : tree) (t2 : tree) : tree =
        requires {valid_tree t1 /\ valid_tree t2}
        variant {t1}
        ensures {valid_tree result}
        ensures {forall k1. (contains t1 k1 \/ contains t2 k1) -> contains result k1}
        match (t1 ,t2) with 
          (Leaf, _) -> t2 
        | (_, Leaf) -> t1 
        | (Node l1 k1 r1, _) ->
            let (l2, _, r2) = split t2 k1 in 
            let l = union l1 l2 in 
            let r = union r1 r2 in 
            joinM l k1 r
        end

end